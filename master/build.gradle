allprojects {
	group = 'org.toadally'
	version = '0.0.1'
	
	repositories {
		mavenCentral()
	}
	
	def externalLibs = fileTree(dir: '../external/lib')
	
	project.ext {
		serverRuntimeDependencies = [
			'javax.enterprise:cdi-api:1.0-SP4',
			'javax.inject:javax.inject:1',
			'javax.persistence:persistence-api:1.0.2',
			'javax.servlet:javax.servlet-api:3.0.1',
			'javax.transaction:transaction-api:1.1',
			'org.jboss.spec.javax.ejb:jboss-ejb-api_3.1_spec:1.0.1.Final'
		]
		
		loggingDependencies = files(externalLibs.matching { includes = ['logback*', 'slf4j*'] })
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	dependencies {
		compile loggingDependencies
		testCompile 'junit:junit:4.8.2'
	}

	test {
		testLogging.showStandardStreams = true
	
		beforeTest { descriptor ->
			logger.lifecycle("Running " + descriptor)
		}
		
		onOutput { descriptor, event ->
			logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
		}
	}
	
	task properties << {
		//project.properties.each { println it.key + ' -> ' + it.value }
		println project.sourceSets.test.java.srcDirs
	}
	
	task depends << {
		println 'compile & runtime dependencies:'
		configurations.runtime.each { File file -> println file.name }
	}
}

